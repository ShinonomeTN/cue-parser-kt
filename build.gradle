plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
}

ext {
    mavenUsername = project.findProperty("maven.username") ?: ""
    mavenPassword = project.findProperty("maven.password") ?: ""
    mavenRepoRelease = project.findProperty("maven.repo.public.release") ?: ""
    mavenRepoSnapshot = project.findProperty("maven.repo.public.snapshot") ?: ""
}

group 'com.shinonometn'
version '2.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

task previewMavenPublishInfo {
    println("Maven User    : $mavenUsername")
    println("Release Repo  : $mavenRepoRelease")
    println("Snapshot Repo : $mavenRepoSnapshot")
}

publishing {
    repositories {
        maven {
            def releasesRepoUrl = "$mavenRepoRelease"
            def snapshotsRepoUrl = "$mavenRepoSnapshot"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username "$mavenUsername"
                password "$mavenPassword"
            }
        }
    }

    publications {
        maven(MavenPublication) {
            artifactId = "cue-parser-kt"

            from components.java

            // Reference : https://kb.novaordis.com/index.php/Gradle_Maven_Publish_Plugin
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}